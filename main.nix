{ pkgs ? import <nixpkgs> { }
, configFile
}:
let
  config = import configFile;

  xdg-desktop-files =
    pkgs.lib.mapAttrsToList
      (
        name: attrs: pkgs.callPackage ./lib/application.nix {
          # inherit (pkgs) writeTextFile lib;
          inherit name attrs;
          header = ''
            # Desktop entry generated by nix-desktop VERSION from
            # ${builtins.toString configFile}
          '';
        }
      )
      (((config.xdg or { }).menu or { }).applications or { });

  systemd-unit-files =
    pkgs.lib.mapAttrsToList
      (
        name: attrs: pkgs.callPackage ./lib/systemd-unit.nix {
          # inherit (pkgs) writeTextFile;
          extension = "service";
          inherit name attrs;
        }
      )
      ((config.systemd or { }).services or { });

  hook-scripts =
    pkgs.callPackage ./lib/hook-scripts.nix {
      systemd = config.systemd or { };
    };
in
pkgs.symlinkJoin {
  name = "${config.name}-desktop-config";
  paths =
    xdg-desktop-files
    ++ systemd-unit-files
    ++ hook-scripts;
}
